macro(addIndent text)
	if(${CMAKE_VERSION} VERSION_GREATER "3.16.0")
		list(APPEND CMAKE_MESSAGE_INDENT ${text})
	endif()
endmacro()

macro(removeIndent)
	if(${CMAKE_VERSION} VERSION_GREATER "3.16.0")
		list(POP_BACK CMAKE_MESSAGE_INDENT)
	endif()
endmacro()

# Macro to get path of first sub dir of a dir, used for MAC OSX lib/header searching
macro(FIRSTSUBDIR result curdir)
	file(GLOB children RELATIVE ${curdir} ${curdir}/*)
	set(dirlist "")
	foreach(child ${children})
		if(IS_DIRECTORY ${curdir}/${child})
			list(APPEND dirlist "${curdir}/${child}")
			break()
		endif()
	endforeach()
	set(${result} ${dirlist})
endmacro()

macro(getTargetImportedLocation result target)
	if(TARGET ${target})
		foreach(prop IMPORTED_LOCATION IMPORTED_LOCATION_NOCONFIG IMPORTED_LOCATION_DEBUG IMPORTED_LOCATION_RELEASE)
			get_target_property (${result} ${target} ${prop})
			if(NOT ("${${result}}" STREQUAL "${result}-NOTFOUND"))
				break()
			endif()
		endforeach()
	endif()
endmacro()
